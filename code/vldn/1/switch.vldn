callback_ms = 100
channel = get_current_channel()

if (channel == "callback") {
    callback_id = get_current_value()
    if (callback_id == 1) {
        set_output("click", false)
    } else if (callback_id == 2) {
        set_output("short_press", false)
    } else if (callback_id == 3) {
        set_output("long_press", false)
    } else if (callback_id == 10) { // hold check
        if (get_output("on") == true) {
            diff_ms = uptime() - get_state("last_change_time_ms")
            if (get_output("hold") and diff_ms >= callback_ms) {
                set_output("hold", false)
                callback(get_config("hold_duration_ms"), 10)
            } else if (!get_output("hold") and diff_ms >= get_config("hold_duration_ms")) {
                set_output("hold", true)
                callback(callback_ms, 10) // schedule callback to switch off the hold pulse
            }
        }
    }
    return
}

pressed = get_current_value()
now = uptime()

if (pressed == 1) {
    set_state("last_change_time_ms", now)
    set_output("on", true)
    set_output("off", false)

    callback(get_config("hold_duration_ms"), 10)
} else {
    diff_ms = uptime() - get_state("last_change_time_ms")

    set_output("toggle", !get_output("toggle"))
    set_output("on", false)
    set_output("off", true)

    if (diff_ms <= get_config("click_duration_ms")) {
        set_output("click", true)
        callback(callback_ms, 1)
    } else if (diff_ms <= get_config("short_press_duration_ms")) {
        set_output("short_press", true)
        callback(callback_ms, 2)
    } else if (diff_ms <= get_config("long_press_duration_ms")) {
        set_output("long_press", true)
        callback(callback_ms, 3)
    }

    set_output("hold", false)
}
